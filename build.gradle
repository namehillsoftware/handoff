plugins {
	id 'java'
	id 'java-library'
	id 'maven-publish'
	id 'signing'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.2'
	id 'com.palantir.git-version' version '0.12.3'
	id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

repositories {
    mavenCentral()
	maven {
		url "https://plugins.gradle.org/m2/"
	}
}

def isRelease = true

// .dockerignore and .gitignore may differ, which can lead to a clean branch looking dirty,
// so use this custom algorithm for version snapshot/not-snapshot detection
version = {
	try {
		def gitDescribe = providers.exec {
			it.ignoreExitValue = true
			commandLine("git", "describe")
		}.standardOutput.asText.get().trim()

		def latestTag = providers.exec {
			it.ignoreExitValue = true
			commandLine("git", "describe", "--abbrev=0")
		}.standardOutput.asText.get().trim()

		if (gitDescribe != latestTag) {
			isRelease = false
			return latestTag + "-SNAPSHOT"
		}

		isRelease = true
		return gitDescribe
	} catch (e) {
		logger.error("An error occurred determining version", e)
		return "0.0.1"
	}
}()

println "Version: $version"

group = "com.namehillsoftware"
archivesBaseName = "handoff"

java {
	withJavadocJar()
	withSourcesJar()
}

sourceSets {
    main {
        java {
            sourceCompatibility = JavaVersion.VERSION_1_7
            targetCompatibility = JavaVersion.VERSION_1_7
        }
    }

    test {
        java {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'org.mockito:mockito-core:2.+'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:2.+'
}

def sonatypeUsername = findProperty("sonatypeUsername")
def sonatypePassword = findProperty("sonatypePassword")
def signingKey = findProperty("signingKey")?.replace("\\n", "\n")
def signingPassword = findProperty("signingPassword")

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId = "com.namehillsoftware"
			artifactId = 'handoff'
			version = version

			from components.java

			pom {
				name = 'handoff'
				description = 'A concise description of my library'
				url = 'https://github.com/namehillsoftware/handoff'
				licenses {
					license {
						name = "MIT License"
						url = "https://spdx.org/licenses/MIT.html"
					}
				}
				developers {
					developer {
						id = 'david'
						name = 'David Vedvick'
						email = 'dvedvick@gmail.com'
					}
				}
				scm {
					url = 'https://github.com/namehillsoftware/handoff'
				}
			}
		}
	}
	repositories {
		maven {
			// change URLs to point to your repos, e.g. http://my.org/repo
			def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
			def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
			url = isRelease ? releasesRepoUrl : snapshotsRepoUrl
			credentials {
				username sonatypeUsername
				password sonatypePassword
			}
		}
	}
}

nexusPublishing {
	repositories {
		sonatype {
			username = sonatypeUsername
			password = sonatypePassword
		}
	}
}

signing {
	required { signingKey?.trim() && gradle.taskGraph.hasTask("publish") }

	useInMemoryPgpKeys(signingKey, signingPassword)
	sign configurations.archives
	sign publishing.publications.mavenJava
}

javadoc {
	if(JavaVersion.current().isJava9Compatible()) {
		options.addBooleanOption('html5', true)
	}
}
